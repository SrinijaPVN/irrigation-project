

#include <Arduino.h>
#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>


const char* ssid = "ram mohan rao";
const char* password = "ramu@2004";
const char* server = "api.thingspeak.com";
const String api_key = "0RFE1PPD5V6XU5EM";


const int moisturePin = A0;
const int triggerPin = D1;
const int echoPin = D2;
const int relayPin = D3;


const int moistureThreshold = 500;
const int waterTankThreshold = 7;


bool isMotorOn = false;


void setup() {
  Serial.begin(9600);
  pinMode(triggerPin, OUTPUT);
  pinMode(echoPin, INPUT);
  pinMode(relayPin, OUTPUT);
  digitalWrite(relayPin, HIGH);


  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi...");
  }
  Serial.println("Connected to WiFi");
}


int getWaterLevel() {
  digitalWrite(triggerPin, LOW);
  delayMicroseconds(2);
  digitalWrite(triggerPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(triggerPin, LOW);


  unsigned long duration = pulseIn(echoPin, HIGH);
  int distance = duration * 0.034 / 2; // Calculate distance in centimeters


  return distance;
}


void sendDataToThingSpeak(int moisture, int waterLevel) {
  WiFiClient client;
  String url = "/update?api_key=" + api_key +
               "&field1=" + String(moisture) +
               "&field2=" + String(waterLevel) +
               "&field3=" + String(isMotorOn); // Include motor status
 
  if (client.connect(server, 80)) {
    client.println("GET " + url + " HTTP/1.1");
    client.println("Host: " + String(server));
    client.println("Connection: close");
    client.println();
    delay(10);
    client.stop();
  }
}


void loop() {
  int moistureValue = analogRead(moisturePin);
  int waterLevel = getWaterLevel();


  Serial.print("Moisture: ");
  Serial.println(moistureValue);
  Serial.print("Water Level: ");
  Serial.println(waterLevel);


  if (moistureValue > moistureThreshold && waterLevel < waterTankThreshold) {
    if (!isMotorOn) {
      digitalWrite(relayPin, LOW);
      delay(5000);
      digitalWrite(relayPin, HIGH);
      isMotorOn = true;
    }
  }
  else {
    digitalWrite(relayPin, HIGH); // Turn off the motor
    isMotorOn = false;
  }


  sendDataToThingSpeak(moistureValue, waterLevel); // Send data to ThingSpeak


  delay(10000);
}
